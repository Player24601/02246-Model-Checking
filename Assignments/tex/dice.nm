dtmc

module Die

  // Local state
  s : [0..7] init 0;
  // Value of the die (0 = undefined)
  d : [0..6] init 0;

  // Flipping the coins
  [] s=0 -> 0.5 : (s'=1) + 0.5 : (s'=2);
  [] s=1 -> 0.5 : (s'=3) + 0.5 : (s'=4);
  [] s=2 -> 0.5 : (s'=5) + 0.5 : (s'=6);
  [] s=3 -> 0.5 : (s'=1) + 0.5 : (s'=7) & (d'=1);
  [] s=4 -> 0.5 : (s'=7) & (d'=2) + 0.5 : (s'=7) & (d'=3);
  [] s=5 -> 0.5 : (s'=7) & (d'=4) + 0.5 : (s'=7) & (d'=5);
  [] s=6 -> 0.5 : (s'=2) + 0.5 : (s'=7) & (d'=6);

  // Absorbing state, after we've decided on the value of the die
  [] s=7 -> 1.0 : (s'=7);
	
endmodule

system
  Die
endsystem